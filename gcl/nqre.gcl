//
// A gcl program to estimate the principal branch of the 
// QRE for data from a normal form game.  
// 
// It is assumed that the normal form is in filename.nfg, 
// and that the data is in the file filename.agg.  
// 
// The first line of the .agg file gives the number of 
// players and the number of strategies for each 
// player.  The second line gives the number of 
// data sets for which the estimation is to be conducted.  
// This should be followed by at least that many sets of data.

// The output is put in filename.out, and the pxifile is put in 
// filename.pxi
// 

Include["nqreml.gcl"]

NewFunction[NQre[filename->TEXT],
  //  Load Normal form game and data
  GetNumericFormat[width<->width:=0,decimals<->decimals:=0];
  GetTextFormat[quote<->quote:=True];
  GetListFormat[lf<->lf:=0];
  n := LoadNfg[filename+".nfg"];
  in := Input[filename+".agg"];

  // discard unused data
  in >> nplrs;
  For[i := 1, i <= nplrs, i := i+1, 
    in >> junk;
  ];

  in >> ndat;

  da:={};
  dat1:=ListForm[Float[Centroid[n]]]; 
  For[i:=1,i<=ndat,i:=i+1,
    in >> dat1;
    da:=da&{dat1};
  ];
  dat:=Mixed[n,da];
  
  // set parameters for NQreML

  min := 0.01;
  max := 30.0;
  del := 0.1;

  pxi := filename+".pxi";
  out := filename+".out";

  NQreML[n,dat,min,max,del,out,pxi];
  SetNumericFormat[width,decimals];
  SetTextFormat[quote];
  SetListFormat[lf->lf];
];


// The following command will run NQre on the game "2x2a.nfg
//
// NQre["2x2a"]



